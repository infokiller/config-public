#compdef install-crossdistro-local-packages
#autoload

: "${REPO_ROOT:="${$(print -r -- "${ZSHENV_DIR}/../.."):P}"}"

_icdlp_list_tasks() {
  local action="${1:-install}"
  local _tasks
  _tasks=("${(f)$("${REPO_ROOT}/install/install-crossdistro-local-packages" list "${action}")}")
  _describe tasks _tasks
}

_icdlp_comp() {
  # for k in "${(k)compstate[@]}"; do
  #   echo "${k} = "${compstate[${k}]}""
  # done
  # echo "${words[@]}"
  # echo
  if ((CURRENT == 1)); then
    local cmds=(list install upgrade audit)
    _describe -t commands 'Commands' cmds
    return
  fi
  case "$words[1]" in
    list)
      _message 'no options'
      ;;
    install|upgrade)
      _arguments \
        '--all[Run all tasks]' \
        '--parallel[Run tasks in parallel]' \
        "*::Tasks: _icdlp_list_tasks ${words[1]}"
      ;;
    audit)
      _message 'no options'
      ;;
  esac
}

_arguments '*::install-crossdistro-local-packages commands:_icdlp_comp'
